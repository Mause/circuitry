[adder [] []]


<sixteen_bit_adder [a1,b1,c1,d1,e1,f1,g1,h1,i1,j1,k1,l1,m1,n1,o1,p1,
                    a2,b2,c2,d2,e2,f2,g2,h2,i2,j2,k2,l2,m2,n2,o2,p2,cin]
                   [a,b,c,d,e,f,g,h,i,j,k,l,m,n,o,p,carry]> {
    [[adder1,adder2] eight_bit_adder]

    self[[a1,b1,c1,d1,e1,f1,g1,h1]] -> adder1[[a1,b1,c1,d1,e1,f1,g1,h1]]
    self[[a2,b2,c2,d2,e2,f2,g2,h2]] -> adder1[[a2,b2,c2,d2,e2,f2,g2,h2]]
    adder1[carry] -> self[carry]

    self[[i1,j1,k1,l1,m1,n1,o1,p1]] -> adder2[[a1,b1,c1,d1,e1,f1,g1,h1]]
    self[[i2,j2,k2,l2,m2,n2,o2,p2]] -> adder2[[a2,b2,c2,d2,e2,f2,g2,h2]]
    adder2[carry] -> adder1[cin]

    adder1[[a,b,c,d,e,f,g,h]] -> self[[a,b,c,d,e,f,g,h]]
    adder2[[a,b,c,d,e,f,g,h]] -> self[[i,j,k,l,m,n,o,p]]
}


<eight_bit_adder [a1,b1,c1,d1,e1,f1,g1,h1,
                  a2,b2,c2,d2,e2,f2,g2,h2,cin]
                 [a,b,c,d,e,f,g,h,carry]> {
    [[adder1, adder2, adder3, adder4, adder5, adder6, adder7, adder8] full_adder]

    # connect the carry lines of the adders
    self[cin] -> adder1[cin]
    adder1[cout] -> adder2[cin]
    adder2[cout] -> adder3[cin]
    adder3[cout] -> adder4[cin]
    adder4[cout] -> adder5[cin]
    adder5[cout] -> adder6[cin]
    adder6[cout] -> adder7[cin]
    adder7[cout] -> adder8[cin]
    adder8[cout] -> self[carry]  # addition carry

    # connect the first input
    self[a1] -> adder8[a]
    self[b1] -> adder7[a]
    self[c1] -> adder6[a]
    self[d1] -> adder5[a]
    self[e1] -> adder4[a]
    self[f1] -> adder3[a]
    self[g1] -> adder2[a]
    self[h1] -> adder1[a]

    # connect the second input
    self[a2] -> adder8[b]
    self[b2] -> adder7[b]
    self[c2] -> adder6[b]
    self[d2] -> adder5[b]
    self[e2] -> adder4[b]
    self[f2] -> adder3[b]
    self[g2] -> adder2[b]
    self[h2] -> adder1[b]

    # connect the output lines
    adder1[s] -> self[a]
    adder2[s] -> self[b]
    adder3[s] -> self[c]
    adder4[s] -> self[d]
    adder5[s] -> self[e]
    adder6[s] -> self[f]
    adder7[s] -> self[g]
    adder8[s] -> self[h]
}


<full_adder [a,b,cin] [s,cout]> {
    [[ha1,ha2] half_adder]
    [orer or]

    self[[a,b]] -> ha1[[a,b]]

    ha1[s] -> ha2[a]
    self[cin] -> ha2[b]
    ha2[s] -> self[s]

    ha1[c] -> orer[a]
    ha2[c] -> orer[b]
    orer[o] -> self[cout]
}


<half_adder [a,b] [c,s]> {
    [c1 xor]
    [c2 and]

    c1[o] -> self[s]
    c2[o] -> self[c]

    self[[a,b]] -> c1[[a,b]]
    self[[a,b]] -> c2[[a,b]]
}
